plugins {
    id 'java'
    id 'maven-publish'
}

group = 'com.trivago.huter'
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    // def hiveVersion = '2.3.7'
    def hiveVersion = '3.1.2'
    // def tezVersion = '0.8.4'
    def tezVersion = '0.9.2'

    compileOnly "org.apache.hive:hive-exec:$hiveVersion:core"
    compileOnly "org.apache.hive:hive-cli:$hiveVersion"
    compileOnly "org.apache.hive:hive-metastore:$hiveVersion"

    compileOnly "org.apache.tez:tez-api:$tezVersion"
    compileOnly "org.apache.tez:tez-runtime-library:$tezVersion"
    compileOnly "org.apache.tez:tez-dag:$tezVersion"
    compileOnly "org.apache.tez:tez-mapreduce:$tezVersion"

    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'info.picocli:picocli:4.6.1'

    // To cover unshaded KryoException in MapJoinOperator imports
    implementation 'com.esotericsoftware:kryo-shaded:4.0.2'
}

configurations.all {
    exclude group: 'ch.qos.logback', module: 'logback-classic'
    exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
    exclude group: 'org.pentaho', module: 'pentaho-aggdesigner-algorithm'
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'Huter'
                description = 'Hive Unit TEst Runner'
                url = 'https://www.github.com/trivago/huter'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'hkoosha'
                        name = 'Koosha Hosseiny'
                        email = 'koosha.hosseiny@trivago.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/trivago/huter.git'
                    developerConnection = 'scm:git:ssh://github.com/trivago/huter.git'
                    url = 'https://github.com/trivago/huter'
                }
            }
            groupId 'com.trivago.huter'
            artifactId 'huter-common'
            version version
            from components.java
            artifact tasks.sourceJar
        }
    }
}
