plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'io.koosha.huter'
version = '2.0'

repositories {
    mavenCentral()
}

dependencies {
    // def hiveVersion = '2.3.7'
    def hiveVersion = '3.1.3'
    // def tezVersion = '0.8.4'
    def tezVersion = '0.10.2'

    compileOnly "org.apache.hive:hive-exec:$hiveVersion:core"
    compileOnly "org.apache.hive:hive-cli:$hiveVersion"
    compileOnly "org.apache.hive:hive-metastore:$hiveVersion"

    compileOnly "org.apache.tez:tez-api:$tezVersion"
    compileOnly "org.apache.tez:tez-runtime-library:$tezVersion"
    compileOnly "org.apache.tez:tez-dag:$tezVersion"
    compileOnly "org.apache.tez:tez-mapreduce:$tezVersion"

    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'info.picocli:picocli:4.7.5'

    // To cover unshaded KryoException in MapJoinOperator imports
    implementation 'com.esotericsoftware:kryo-shaded:4.0.3'
}

configurations.configureEach {
    exclude group: 'ch.qos.logback', module: 'logback-classic'
    exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
    exclude group: 'org.pentaho', module: 'pentaho-aggdesigner-algorithm'
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'Huter'
                description = 'Hive Unit TEst Runner'
                url = 'https://www.github.com/hkoosha/huter'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'hkoosha'
                        name = 'Koosha Hosseiny'
                        email = 'i@koosha.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/hkoosha/huter.git'
                    developerConnection = 'scm:git:ssh://github.com/hkoosha/huter.git'
                    url = 'https://github.com/hkoosha/huter'
                }
            }
            groupId 'io.koosha.huter'
            artifactId 'huter-common'
            version version
            from components.java
        }
    }
}

//signing {
//    sign publishing.publications.mavenJava
//}
